# Copyright (c) 2025 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("//build/config/components/idl_tool/idl.gni")
import("//foundation/systemabilitymgr/selectionfwk/selection_service.gni")

config("selection_listener_config") {
  include_dirs = [
    "include",
    "../../../utils/include",
    "${target_gen_dir}",
    "${selection_fwk_root_path}/common",
    "${selection_fwk_root_path}/frameworks/common",
  ]
}

idl_interface_sources = [
  "${target_gen_dir}/selection_listener_proxy.cpp",
  "${target_gen_dir}/selection_listener_stub.cpp",
]

idl_gen_interface("selection_listener_interface") {
  src_idl = rebase_path("ISelectionListener.idl")
  dst_file = string_join(",", idl_interface_sources)
}

ohos_source_set("selection_listener_proxy") {
  sanitize = {
    cfi = true
    cfi_cross_dso = true
    debug = false
  }
  include_dirs = [
    "${target_gen_dir}",
  ]

  public_configs = [":selection_listener_config"]
  output_values = get_target_outputs(":selection_listener_interface")
  sources = filter_include(output_values, [ "*_proxy.cpp" ])
  deps = [ ":selection_listener_interface" ]
  external_deps = [
    "c_utils:utils",
    "hilog:libhilog",
    "ipc:ipc_single",
  ]
  part_name = "selectionfwk"
  subsystem_name = "systemabilitymgr"
}

ohos_shared_library("selection_ability") {
  branch_protector_ret = "pac_ret"
  sanitize = {
    boundary_sanitize = true
    cfi = true
    cfi_cross_dso = true
    cfi_vcall_icall_only = true
    debug = false
    integer_overflow = true
    ubsan = true
  }
  configs = [ ":selection_listener_config", ]
  output_values = get_target_outputs(":selection_listener_interface")

  sources = [
    "src/selection_ability.cpp",
    "src/selection_listener_impl.cpp",
    "src/selection_panel.cpp",
    "src/task_manager.cpp",
    "src/tasks/task.cpp",
  ]
  sources += filter_include(output_values, [ "*.cpp" ])
  deps = [
    "${selection_fwk_root_path}/common:selection_common",
    ":selection_listener_interface",
  ]

  external_deps = [
    "c_utils:utils",
    "ipc:ipc_single",
    "hilog:libhilog",
    "ability_base:configuration",
    "ability_base:want",
    "ability_runtime:ability_context_native",
    "bundle_framework:appexecfwk_base",
    "config_policy:configpolicy_util",
    "eventhandler:libeventhandler",
    "graphic_2d:librender_service_base",
    "graphic_2d:librender_service_client",
    "graphic_2d:window_animation",
    "input:libmmi-client",
    "safwk:system_ability_fwk",
    "samgr:samgr_proxy",
    "window_manager:libdm",
    "window_manager:libwsutils",
  ]

  public_external_deps = [ "window_manager:libwm" ]

  part_name = "selectionfwk"
  subsystem_name = "systemabilitymgr"
}