# Copyright (c) 2025 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
# http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
import("//build/config/components/ets_frontend/ets2abc_config.gni")
import("//build/ohos.gni")
import("//build/ohos/taihe_idl/taihe.gni")
import("//foundation/systemabilitymgr/selectionfwk/selection_service.gni")

copy_taihe_idl("copy_selectionManager") {
  sources = [ "idl/ohos.selectionInput.selectionManager.taihe" ] 
}
subsystem_name = "systemabilitymgr"
part_name = "selectionfwk"
taihe_generated_file_path_selectionManager = "$taihe_file_path/out/$subsystem_name/$part_name/selectionManager"

ohos_taihe("run_taihe") {
  taihe_generated_file_path = "${taihe_generated_file_path_selectionManager}"
  deps = [ ":copy_selectionManager" ]
  outputs = [
    "$taihe_generated_file_path/src/ohos.selectionInput.selectionManager.ani.cpp",
    "$taihe_generated_file_path/src/ohos.selectionInput.selectionManager.abi.c",
  ]
}

taihe_shared_library("selectionManager_taihe_native") {
  sanitize = {
    cfi = true
    cfi_cross_dso = true
    debug = false
  }
  taihe_generated_file_path = "${taihe_generated_file_path_selectionManager}"
  part_name = "$part_name"
  subsystem_name = "$subsystem_name"
  sources = get_target_outputs(":run_taihe")
  include_dirs = [
    "include",
    "${selection_fwk_root_path}/utils/include",
    "../../../native/selection_ability/include",
    "${selection_fwk_root_path}/interfaces/inner_kits/selection_client/include",
  ]
  sources += [
    "src/ani_constructor.cpp",
    "src/ohos.selectionInput.selectionManager.impl.cpp",
    "src/ets_selection_engine_setting.cpp",
    "src/panel_listener_impl.cpp",
  ]
  deps = [
    ":run_taihe",
    "${selection_fwk_root_path}/interfaces/idl:selection_service_interface",
    "${selection_fwk_root_path}/common:selection_common",
    "${selection_fwk_root_path}/interfaces/idl:selection_service_stub",
    "${selection_fwk_root_path}/interfaces/idl:selection_listener_proxy",
    "${selection_fwk_root_path}/frameworks/native/selection_ability:selection_ability",
    "${selection_fwk_root_path}/interfaces/inner_kits/selection_client:selection_client",
  ]

  external_deps = [
    "c_utils:utils",
    "hilog:libhilog_base",
    "ipc:ipc_single",
    "runtime_core:ani",
    "samgr:samgr_proxy",
    "ability_runtime:abilitykit_native",
    "graphic_2d:librender_service_client",
    "ability_runtime:ani_base_context",
  ]
  if (defined(global_parts_info) &&
      defined(global_parts_info.bundlemanager_bundle_framework)) {
    external_deps += [ "bundle_framework:appexecfwk_base" ]
    defines = [ "DEPENDENT_APPEXECFWK_BASE" ]
  }
}

generate_static_abc("selectionManager") {
  base_url = "$taihe_generated_file_path_selectionManager"
  files = [ "$taihe_generated_file_path_selectionManager/@ohos.selectionInput.selectionManager.ets" ]
  is_boot_abc = "True"
  device_dst_file = "/system/framework/selectionManager.abc"
  dependencies = [ ":run_taihe" ]
}

ohos_prebuilt_etc("selectionManager_etc") {
  source = "$target_out_dir/selectionManager.abc"           
  module_install_dir = "framework"
  part_name = "$part_name"
  subsystem_name = "$subsystem_name"
  deps = [ ":selectionManager" ]
}

group("selectionManager_taihe") {
    deps = [
      ":selectionManager_taihe_native",
      ":selectionManager_etc",
    ]
}
