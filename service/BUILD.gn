# Copyright (c) 2025 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("//build/config/components/idl_tool/idl.gni")
import("//foundation/systemabilitymgr/selection_fwk/selection_service.gni")

config("selection_sa_config") {
  # visibility = [ ":*" ]
  include_dirs = [
    "include",
    "../utils/include",
    "${target_gen_dir}",
  ]
}

idl_interface_sources = [
  "${target_gen_dir}/selection_service_proxy.cpp",
  "${target_gen_dir}/selection_service_stub.cpp",
]

idl_gen_interface("selection_service_interface") {
  src_idl = rebase_path("ISelectionService.idl")
  dst_file = string_join(",", idl_interface_sources)
}

ohos_source_set("selection_service_proxy") {
  sanitize = {
    cfi = true
    cfi_cross_dso = true
    cfi_vcall_icall_only = true
    debug = false
  }
  include_dirs = [
    "${target_gen_dir}",
  ]

  public_configs = [":selection_sa_config"]
  output_values = get_target_outputs(":selection_service_interface")
  sources = filter_include(output_values, [ "*_proxy.cpp" ])
  deps = [ ":selection_service_interface" ]
  external_deps = [
    "c_utils:utils",
    "hilog:libhilog",
    "ipc:ipc_single",
  ]
  part_name = "selectionfwk"
  subsystem_name = "systemabilitymgr"
}

ohos_shared_library("selection_service") {
  branch_protector_ret = "pac_ret"
  sanitize = {
    boundary_sanitize = true
    cfi = true
    cfi_cross_dso = true
    cfi_vcall_icall_only = true
    debug = false
    integer_overflow = true
    ubsan = true
  }
  cflags_cc = [
    "-fdata-sections",
    "-ffunction-sections",
    "-Oz",
  ]
  configs = [ ":selection_sa_config", ]
  output_values = get_target_outputs(":selection_service_interface")

  include_dirs = [
    "./focus_monitor/include",
  ]

  sources = [
    "src/selection_service.cpp",
    "src/selection_input_monitor.cpp",
    "./focus_monitor/src/focus_change_listener.cpp",
    "./focus_monitor/src/focus_monitor_manager.cpp",
  ]
  sources += filter_include(output_values, [ "*.cpp" ])
  deps = [
    ":selection_service_interface",
    "${selection_fwk_root_path}/common:selection_common",
    "${selection_fwk_root_path}/frameworks/native/selection_ability:selection_listener_proxy",
  ]

  external_deps = [
    "ability_base:want",
    "ability_runtime:ability_manager",
    "c_utils:utils",
    "napi:ace_napi",
    "hilog:libhilog",
    "ipc:ipc_single",
    "init:libbeget_proxy",
    "input:libmmi-client",
    "safwk:system_ability_fwk",
    "samgr:samgr_proxy",
    "screenlock_mgr:screenlock_client",
    "input:libmmi-client",
    "window_manager:libdm_lite",
    "window_manager:libwsutils",
  ]

  if (window_manager_use_sceneboard) {
    external_deps += [ "window_manager:libwm_lite" ]
    defines += [ "SCENE_BOARD_ENABLE" ]
  } else {
    external_deps += [ "window_manager:libwm" ]
  }

  part_name = "selectionfwk"
  subsystem_name = "systemabilitymgr"
}